/*
    SPDX-FileCopyrightText: 2025-2026 Vitaliy Elin <daydve@smbit.pro>
    SPDX-FileCopyrightText: 2013 Eike Hein <hein@kde.org>

    SPDX-License-Identifier: GPL-2.0-or-later
*/

import QtQuick
import QtQuick.Controls
import QtQuick.Layouts

import org.kde.kirigami as Kirigami
import org.kde.plasma.core as PlasmaCore
import org.kde.plasma.plasmoid
import org.kde.kquickcontrols as KQuickAddons

import "../ui/code/singletones"

ConfigPage {
    id: cfg_page
    
    // Silence KCM errors for legacy/removed properties
    readonly property bool plasmaPaAvailable: true
    readonly property bool plasmoidVertical: Plasmoid.formFactor === PlasmaCore.Types.Vertical
    readonly property bool iconOnly: Plasmoid.configuration.iconOnly

    ColumnLayout {
        anchors.fill: parent
        spacing: Kirigami.Units.largeSpacing

        LivePreview {
            cfg_page: cfg_page
            location: Plasmoid.location
            Layout.fillWidth: true
            visible: Plasmoid.location !== PlasmaCore.Types.Floating
        }

        ConfigScrollView {

                Kirigami.FormLayout {
                    width: parent.width - Kirigami.Units.gridUnit * 2

                CheckBox {
                id: useBorders
                text: Wrappers.i18n("Use plasma borders")
                checked: cfg_page.cfg_useBorders
                onToggled: cfg_page.cfg_useBorders = checked
            }

            Item { height: Kirigami.Units.largeSpacing }

            Label {
                text: Wrappers.i18n("Display:")
            }
            ComboBox {
                id: cfg_iconOnly
                Layout.fillWidth: true
                Layout.minimumWidth: Kirigami.Units.gridUnit * 14
                model: [Wrappers.i18n("Classic panel"), Wrappers.i18n("Show icons only")]
                currentIndex: cfg_page.cfg_iconOnly
                onActivated: (index) => cfg_page.cfg_iconOnly = index
            }

            Item { height: Kirigami.Units.largeSpacing }

            RowLayout {
                spacing: Kirigami.Units.smallSpacing
                Label {
                    text: Wrappers.i18n("Icon size:")
                }
                ComboBox {
                    id: iconSizeOverrideCombo
                    Layout.fillWidth: true
                    model: [Wrappers.i18n("Relative"), Wrappers.i18n("Absolute")]
                    currentIndex: cfg_page.cfg_iconSizeOverride ? 1 : 0
                    onActivated: (index) => cfg_page.cfg_iconSizeOverride = (index === 1)
                }
            }

            RowLayout {
                Layout.fillWidth: true
                visible: !cfg_page.cfg_iconSizeOverride
                spacing: Kirigami.Units.smallSpacing

                Slider {
                    id: iconScale
                    Layout.fillWidth: true
                    from: 0
                    to: 300
                    stepSize: 1.0
                    value: cfg_page.cfg_iconScale
                    onMoved: cfg_page.cfg_iconScale = value
                }

                SpinBox {
                    id: iconScaleSpin
                    from: 0
                    to: 300
                    editable: true
                    value: Math.round(iconScale.value)
                    onValueModified: cfg_page.cfg_iconScale = value
                }

                Label {
                    text: "%"
                }

                Button {
                    icon.name: "edit-reset"
                    flat: true
                    onClicked: cfg_page.cfg_iconScale = 100
                    ToolTip.text: Wrappers.i18n("Reset to default")
                    ToolTip.visible: hovered
                    ToolTip.delay: 1000
                }
            }

            RowLayout {
                Layout.fillWidth: true
                visible: cfg_page.cfg_iconSizeOverride
                spacing: Kirigami.Units.smallSpacing

                Slider {
                    id: iconSizePx
                    Layout.fillWidth: true
                    from: 0
                    to: 100
                    stepSize: 1
                    value: cfg_page.cfg_iconSizePx
                    onMoved: cfg_page.cfg_iconSizePx = value
                }

                SpinBox {
                    id: iconSizePxSpin
                    from: 0
                    to: 100
                    editable: true
                    value: iconSizePx.value
                    onValueModified: cfg_page.cfg_iconSizePx = value
                }

                Label {
                    text: "px"
                }

                Button {
                    icon.name: "edit-reset"
                    flat: true
                    onClicked: cfg_page.cfg_iconSizePx = 32
                    ToolTip.text: Wrappers.i18n("Reset to default")
                    ToolTip.visible: hovered
                    ToolTip.delay: 1000
                }
            }

            CheckBox {
                id: iconScaleFromEdge
                text: Wrappers.i18n("Scale icons from panel edge")
                checked: cfg_page.cfg_iconScaleFromEdge
                onToggled: cfg_page.cfg_iconScaleFromEdge = checked
            }

            RowLayout {
                visible: iconScaleFromEdge.checked
                spacing: Kirigami.Units.smallSpacing
                Label {
                    text: Wrappers.i18n("Edge offset (px):")
                }
                SpinBox {
                    id: iconEdgeOffset
                    from: 0
                    to: 15
                    stepSize: 1
                    value: cfg_page.cfg_iconEdgeOffset
                    onValueModified: cfg_page.cfg_iconEdgeOffset = value
                }
            }

            RowLayout {
                spacing: Kirigami.Units.smallSpacing
                Label {
                    text: Wrappers.i18n("Button corner radius (px):")
                }
                SpinBox {
                    id: cfg_customHoverOverlayRadius
                    from: 0
                    to: 50
                    stepSize: 1
                    value: cfg_page.cfg_customHoverOverlayRadius
                    onValueModified: cfg_page.cfg_customHoverOverlayRadius = value
                }
            }

            Item { 
                height: Kirigami.Units.largeSpacing 
            }

            CheckBox {
                id: cfg_taskHoverEffect
                text: Wrappers.i18n("Task hover effects")
                checked: cfg_page.cfg_taskHoverEffect
                onToggled: cfg_page.cfg_taskHoverEffect = checked
            }

            RowLayout {
                visible: cfg_taskHoverEffect.checked
                Item { implicitWidth: Kirigami.Units.gridUnit }
                ColumnLayout {
                    spacing: Kirigami.Units.smallSpacing

                    CheckBox {
                        id: cfg_iconHoverBrighten
                        text: Wrappers.i18n("Brighten icon on hover")
                        checked: cfg_page.cfg_iconHoverBrighten
                        onToggled: cfg_page.cfg_iconHoverBrighten = checked
                    }

                    CheckBox {
                        id: cfg_customHoverOverlayEnabled
                        text: Wrappers.i18n("Highlight button with white overlay")
                        checked: cfg_page.cfg_customHoverOverlayEnabled
                        onToggled: cfg_page.cfg_customHoverOverlayEnabled = checked
                    }

                    RowLayout {
                        visible: cfg_customHoverOverlayEnabled.checked
                        Item { implicitWidth: Kirigami.Units.gridUnit }
                        spacing: Kirigami.Units.smallSpacing
                        Label {
                            text: Wrappers.i18n("Overlay opacity (%):")
                        }
                        SpinBox {
                            id: cfg_customHoverOverlayOpacity
                            from: 1
                            to: 100
                            stepSize: 1
                            value: cfg_page.cfg_customHoverOverlayOpacity
                            onValueModified: cfg_page.cfg_customHoverOverlayOpacity = value
                        }
                    }

                    RowLayout {
                        visible: cfg_page.cfg_iconOnly === 1
                        spacing: Kirigami.Units.smallSpacing
                        Label {
                            text: Wrappers.i18n("Icon zoom effect:")
                        }
                        ComboBox {
                            id: cfg_taskHoverEffectStyle
                            Layout.fillWidth: true
                            Layout.minimumWidth: Kirigami.Units.gridUnit * 14
                            model: [
                                Wrappers.i18n("Simple"),
                                Wrappers.i18n("Parabolic")
                            ]
                            currentIndex: cfg_page.cfg_taskHoverEffectStyle
                            onActivated: (index) => cfg_page.cfg_taskHoverEffectStyle = index
                        }
                    }

                    RowLayout {
                        visible: cfg_page.cfg_iconOnly === 1
                        spacing: Kirigami.Units.smallSpacing
                        Label {
                            text: Wrappers.i18n("Icon zoom factor (px):")
                        }
                        SpinBox {
                            id: iconZoomFactor
                            from: 0
                            to: 50
                            stepSize: 1
                            value: cfg_page.cfg_iconZoomFactor
                            onValueModified: cfg_page.cfg_iconZoomFactor = value

                            ToolTip.delay: 1000
                            ToolTip.visible: hovered
                            ToolTip.text: Wrappers.i18n("How much the icon should grow when hovered (in pixels)")
                        }
                    }

                    RowLayout {
                        visible: cfg_page.cfg_iconOnly === 1
                        spacing: Kirigami.Units.smallSpacing
                        Label {
                            text: Wrappers.i18n("Zoom animation duration (ms):")
                        }
                        SpinBox {
                            id: iconZoomDuration
                            from: 0
                            to: 1000
                            stepSize: 50
                            value: cfg_page.cfg_iconZoomDuration
                            onValueModified: cfg_page.cfg_iconZoomDuration = value

                            ToolTip.delay: 1000
                            ToolTip.visible: hovered
                            ToolTip.text: Wrappers.i18n("Duration of the zoom animation in milliseconds")
                        }
                    }
                }
            }

            Item { height: Kirigami.Units.largeSpacing }

            CheckBox {
                id: cfg_disableButtonSvg
                text: Wrappers.i18n("Disable plasma context decorations")
                checked: cfg_page.cfg_disableButtonSvg
                onToggled: cfg_page.cfg_disableButtonSvg = checked
            }

            Item { height: Kirigami.Units.largeSpacing }

            Label {
                text: Wrappers.i18n("Button Colors:")
                enabled: !cfg_disableButtonSvg.checked
            }

            RowLayout {
                spacing: Kirigami.Units.smallSpacing
                Layout.fillWidth: true
                
                ComboBox {
                    id: buttonColorCombo
                    Layout.fillWidth: true
                    enabled: !cfg_disableButtonSvg.checked
                    model: [
                        Wrappers.i18n("Using Plasma Style/Accent"),
                        Wrappers.i18n("Use dominant icon color"),
                        Wrappers.i18n("Custom color")
                    ]
                    currentIndex: {
                        if (!cfg_page.cfg_buttonColorize) return 0;
                        if (cfg_page.cfg_buttonColorizeDominant) return 1;
                        return 2;
                    }
                    onActivated: index => {
                        if (index === 0) {
                            cfg_page.cfg_buttonColorize = false;
                            cfg_page.cfg_buttonColorizeDominant = false;
                        } else if (index === 1) {
                            cfg_page.cfg_buttonColorize = true;
                            cfg_page.cfg_buttonColorizeDominant = true;
                        } else if (index === 2) {
                            cfg_page.cfg_buttonColorize = true;
                            cfg_page.cfg_buttonColorizeDominant = false;
                        }
                    }
                }

                KQuickAddons.ColorButton {
                    id: cfg_buttonColorizeCustom
                    showAlphaChannel: true
                    enabled: !cfg_disableButtonSvg.checked
                    visible: cfg_page.cfg_buttonColorize && !cfg_page.cfg_buttonColorizeDominant
                    Layout.maximumHeight: buttonColorCombo.height
                    color: cfg_page.cfg_buttonColorizeCustom
                    onColorChanged: {
                        if (!Qt.colorEqual(color, cfg_page.cfg_buttonColorizeCustom)) {
                            cfg_page.cfg_buttonColorizeCustom = color
                        }
                    }
                }
            }

            Item { height: Kirigami.Units.largeSpacing }

            Label {
                text: Wrappers.i18n("For inactive buttons:")
                enabled: !cfg_disableButtonSvg.checked
            }

            CheckBox {
                id: cfg_disableButtonInactiveSvg
                text: Wrappers.i18n("Hide backgrounds for inactive buttons")
                enabled: !cfg_disableButtonSvg.checked
                checked: cfg_page.cfg_disableButtonInactiveSvg
                onToggled: cfg_page.cfg_disableButtonInactiveSvg = checked
            }

            CheckBox {
                id: cfg_buttonColorizeInactive
                text: Wrappers.i18n("Colorize inactive buttons")
                enabled: !cfg_disableButtonSvg.checked && cfg_page.cfg_buttonColorize && !cfg_disableButtonInactiveSvg.checked
                checked: cfg_page.cfg_buttonColorizeInactive
                onToggled: cfg_page.cfg_buttonColorizeInactive = checked
            }

            Item { height: Kirigami.Units.largeSpacing }

            Label {
                visible: cfg_page.cfg_iconOnly === 0 && !cfg_page.plasmoidVertical
                text: Wrappers.i18n("Maximum button width (px):")
            }
            SpinBox {
                id: maxButtonLength
                visible: cfg_page.cfg_iconOnly === 0 && !cfg_page.plasmoidVertical
                from: 40
                to: 1000
                value: cfg_page.cfg_maxButtonLength
                onValueModified: cfg_page.cfg_maxButtonLength = value
            }

            RowLayout {
                spacing: Kirigami.Units.smallSpacing
                Label {
                    text: Wrappers.i18n("Space between taskbar items (px):")
                }
                SpinBox {
                    id: taskSpacingSize
                    from: 0
                    to: 99
                    value: cfg_page.cfg_taskSpacingSize
                    onValueModified: cfg_page.cfg_taskSpacingSize = value
                }
            }

            Item { height: Kirigami.Units.largeSpacing }

            Label {
                text: Wrappers.i18n("Icon Shape:")
            }

            CheckBox {
                id: clipIconToShape
                text: Wrappers.i18n("Clip icons to a custom shape")
                checked: cfg_page.cfg_clipIconToShape
                onToggled: cfg_page.cfg_clipIconToShape = checked
            }

            RowLayout {
                visible: cfg_page.cfg_clipIconToShape
                spacing: Kirigami.Units.smallSpacing
                Label {
                    text: Wrappers.i18n("Icon corner radius:")
                }
                Slider {
                    id: iconClipRadiusSlider
                    Layout.fillWidth: true
                    from: 0
                    to: 100
                    stepSize: 1
                    value: cfg_page.cfg_iconClipRadius
                    onMoved: cfg_page.cfg_iconClipRadius = value
                }
                SpinBox {
                    id: iconClipRadiusSpin
                    from: 0
                    to: 100
                    editable: true
                    value: iconClipRadiusSlider.value
                    onValueModified: cfg_page.cfg_iconClipRadius = value
                    textFromValue: function(value, locale) { return value + "%" }
                    valueFromText: function(text, locale) { return parseInt(text) }
                }
            }

            CheckBox {
                id: clipIconBackgroundEnabled
                visible: cfg_page.cfg_clipIconToShape
                text: Wrappers.i18n("Show background under clipped icons")
                checked: cfg_page.cfg_clipIconBackgroundEnabled
                onToggled: cfg_page.cfg_clipIconBackgroundEnabled = checked
            }

            Label {
                visible: cfg_page.cfg_clipIconToShape && cfg_page.cfg_clipIconBackgroundEnabled
                text: Wrappers.i18n("Background color source:")
            }

            RowLayout {
                visible: cfg_page.cfg_clipIconToShape && cfg_page.cfg_clipIconBackgroundEnabled
                spacing: Kirigami.Units.smallSpacing
                Layout.fillWidth: true
                ComboBox {
                    id: cfg_clipIconBackgroundColorMode
                    Layout.fillWidth: true
                    model: [
                        Wrappers.i18n("Custom color"),
                        Wrappers.i18n("Dominant icon color"),
                        Wrappers.i18n("Average icon color"),
                        Wrappers.i18n("Plasma accent color")
                    ]
                    currentIndex: cfg_page.cfg_clipIconBackgroundColorMode
                    onActivated: (index) => cfg_page.cfg_clipIconBackgroundColorMode = index
                }

                KQuickAddons.ColorButton {
                    id: clipIconBackgroundColorBtn
                    visible: cfg_page.cfg_clipIconBackgroundColorMode === 0
                    showAlphaChannel: true
                    Layout.maximumHeight: cfg_clipIconBackgroundColorMode.height
                    color: cfg_page.cfg_clipIconBackgroundColor
                    onColorChanged: {
                        if (!Qt.colorEqual(color, cfg_page.cfg_clipIconBackgroundColor)) {
                            cfg_page.cfg_clipIconBackgroundColor = color
                        }
                    }
                }
            }

            RowLayout {
                visible: cfg_page.cfg_clipIconToShape && cfg_page.cfg_clipIconBackgroundEnabled
                spacing: Kirigami.Units.smallSpacing
                Label {
                    text: Wrappers.i18n("Background opacity:")
                }
                Slider {
                    id: clipIconBackgroundOpacitySlider
                    Layout.fillWidth: true
                    from: 0
                    to: 100
                    stepSize: 5
                    value: cfg_page.cfg_clipIconBackgroundOpacity
                    onMoved: cfg_page.cfg_clipIconBackgroundOpacity = value
                }
                SpinBox {
                    id: clipIconBackgroundOpacitySpin
                    from: 0
                    to: 100
                    editable: true
                    value: clipIconBackgroundOpacitySlider.value
                    onValueModified: cfg_page.cfg_clipIconBackgroundOpacity = value
                    textFromValue: function(value, locale) { return value + "%" }
                    valueFromText: function(text, locale) { return parseInt(text) }
                }
            }

            Item { height: Kirigami.Units.largeSpacing }

            Label {
                text: cfg_page.plasmoidVertical ? Wrappers.i18n("Use multi-column view:") : Wrappers.i18n("Use multi-row view:")
            }

            RadioButton {
                id: forbidStripes
                text: Wrappers.i18n("Never")
                checked: cfg_page.cfg_maxStripes === 1
                onToggled: {
                    if (checked) {
                        cfg_page.cfg_maxStripes = 1;
                    }
                }
            }

            RadioButton {
                id: allowStripes
                text: Wrappers.i18n("When panel is low on space and thick enough")
                checked: cfg_page.cfg_maxStripes > 1 && !cfg_page.cfg_forceStripes
                onToggled: {
                    if (checked) {
                        cfg_page.cfg_maxStripes = Math.max(2, cfg_page.cfg_maxStripes);
                        cfg_page.cfg_forceStripes = false;
                    }
                }
            }

            RadioButton {
                id: forceStripes
                text: Wrappers.i18n("Always when panel is thick enough")
                checked: cfg_page.cfg_maxStripes > 1 && cfg_page.cfg_forceStripes
                onToggled: {
                    if (checked) {
                        cfg_page.cfg_maxStripes = Math.max(2, cfg_page.cfg_maxStripes);
                        cfg_page.cfg_forceStripes = true;
                    }
                }
            }

            Label {
                visible: cfg_page.cfg_maxStripes > 1
                text: cfg_page.plasmoidVertical ? Wrappers.i18n("Maximum columns:") : Wrappers.i18n("Maximum rows:")
            }
            SpinBox {
                id: maxStripes
                visible: cfg_page.cfg_maxStripes > 1
                from: 1
                value: cfg_page.cfg_maxStripes
                onValueModified: cfg_page.cfg_maxStripes = value
            }

            Item { height: Kirigami.Units.largeSpacing }

            RowLayout {
                visible: true
                spacing: Kirigami.Units.smallSpacing
                Label {
                    text: Wrappers.i18n("Inner padding:")
                }
                ComboBox {
                    model: [
                        {
                            "label": Wrappers.i18n("Small"),
                            "spacing": 0
                        },
                        {
                            "label": Wrappers.i18n("Normal"),
                            "spacing": 1
                        },
                        {
                            "label": Wrappers.i18n("Large"),
                            "spacing": 2
                        },
                        {
                            "label": Wrappers.i18n("Huge"),
                            "spacing": 3
                        },
                    ]

                    textRole: "label"
                    visible: !Kirigami.Settings.tabletMode

                    currentIndex: {
                        if (Kirigami.Settings.tabletMode) {
                            return 3; // Large
                        }

                        switch (cfg_page.cfg_iconSpacing) {
                        case 0:
                            return 0; // Small
                        case 1:
                            return 1; // Normal
                        case 2:
                            return 2; // Medium
                        case 3:
                            return 3; // Large
                        }
                    }
                    onActivated: index => {
                        cfg_page.cfg_iconSpacing = model[currentIndex]["spacing"];
                    }
                }
            }

            Label {
                visible: Kirigami.Settings.tabletMode
                text: Wrappers.i18n("Automatically set to Large when in Touch mode")
                font: Kirigami.Theme.smallFont
            }
            } // FormLayout
        } // ConfigScrollView
    } // ColumnLayout
}
